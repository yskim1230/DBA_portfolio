## 전체 DB 인덱스 조각화율 확인 및 리빌드 운영 문서

---
### 개요
주요 프로세스, 서비스 내역을 지원하는 테이블에서 인덱스 조각화율이 증가함을 확인

개선을 위해 서버 인프라 작업 시 인덱스 조각화율이 높은 인덱스들 리빌드를 계획

### 작업 절차

* **전체 사용자 DB를 순회**하며 인덱스 조각화율(Fragmentation)을 수집한다.
* 조각화율 기준으로 **리빌드 대상 목록을 산출**하고,
* 리빌드 전 **DB 백업을 수행**한 뒤,
* 리빌드 실행 시 **실제 소요시간을 기록**하여 단계(1~5차)로 순차 적용한다.

---

## 1) 전체 DB 인덱스 조각화율 확인 스크립트

```sql
-- 전체 DB에서 인덱스 조각화율 확인

DECLARE @DatabaseName AS NVARCHAR(128)
DECLARE @Command AS NVARCHAR(MAX)

-- 결과를 저장할 테이블 생성
CREATE TABLE #FragmentedIndexes (
    DatabaseName NVARCHAR(128),
    SchemaName NVARCHAR(128),
    TableName NVARCHAR(128),
    IndexName NVARCHAR(128),
    Fragmentation FLOAT,
    PageCount INT,
    RowNum BIGINT,
    UserSeeks BIGINT,
    UserScans BIGINT,
    UserLookups BIGINT,
    UserUpdates BIGINT
)

-- 데이터베이스 이름을 순회할 커서 선언
DECLARE db_cursor CURSOR FOR 
SELECT name 
FROM sys.databases
WHERE state_desc = 'ONLINE'
  AND name NOT IN ('master', 'tempdb', 'model', 'msdb') -- 시스템 DB 제외

-- 커서 시작
OPEN db_cursor  
FETCH NEXT FROM db_cursor INTO @DatabaseName  

WHILE @@FETCH_STATUS = 0  
BEGIN  
    -- 각 DB에 대한 쿼리 생성
    SET @Command = 'USE [' + @DatabaseName + ']; 
    INSERT INTO #FragmentedIndexes
    SELECT ''' + @DatabaseName + ''' AS DatabaseName,
           dbschemas.name AS SchemaName,
           dbtables.name AS TableName,
           dbindexes.name AS IndexName,
           indexstats.avg_fragmentation_in_percent AS Fragmentation,
           indexstats.page_count AS PageCount,
           p.rows AS RowNum,
           COALESCE(ius.user_seeks, 0) AS UserSeeks,
           COALESCE(ius.user_scans, 0) AS UserScans,
           COALESCE(ius.user_lookups, 0) AS UserLookups,
           COALESCE(ius.user_updates, 0) AS UserUpdates
    FROM sys.dm_db_index_physical_stats(DB_ID(), NULL, NULL, NULL, ''LIMITED'') AS indexstats
    INNER JOIN sys.tables dbtables
            ON dbtables.object_id = indexstats.object_id
    INNER JOIN sys.schemas dbschemas
            ON dbtables.schema_id = dbschemas.schema_id
    INNER JOIN sys.indexes dbindexes
            ON dbindexes.object_id = indexstats.object_id
           AND indexstats.index_id = dbindexes.index_id
    INNER JOIN sys.partitions p
            ON p.object_id = dbtables.object_id
           AND p.index_id = indexstats.index_id
    LEFT JOIN sys.dm_db_index_usage_stats ius
           ON ius.object_id = indexstats.object_id 
          AND ius.index_id = indexstats.index_id
          AND ius.database_id = DB_ID()
    WHERE indexstats.avg_fragmentation_in_percent > 75
      AND dbindexes.name IS NOT NULL;'

    -- 동적 SQL 실행
    EXEC sp_executesql @Command

    FETCH NEXT FROM db_cursor INTO @DatabaseName  
END  

CLOSE db_cursor  
DEALLOCATE db_cursor

-- 최종 결과 조회
SELECT *
FROM #FragmentedIndexes
ORDER BY DatabaseName, Fragmentation DESC

-- 임시 테이블 삭제
DROP TABLE #FragmentedIndexes
```

---

## 2) 1~5차 리빌드 순차 적용 리스트 작성 기준

### 분류 기준(예시)

* **1초 미만**
* **2초 미만**
* **3초 미만**
* **3초 이상**

### 운영 원칙

* **1차**: 1초 미만 대상부터 적용 (가장 안전/영향 최소)
* **2차**: 2초 미만 대상
* **3차**: 3초 미만 대상
* **4차**: 3초 이상(중대상) 중 우선순위 높은 것
* **5차**: 잔여(대상 재평가 후 필요 시 재조정)

---

## 3) 리빌드 전 대상 DB 백업

```sql
-- 리빌드 전 대상 DB 백업

USE master
GO
BACKUP DATABASE Chunjae_ACA
  TO DISK='S:\Backup_db\Chunjae_ACA\Chunjae_ACA_backup_20250405.bak'
  WITH COMPRESSION, INIT;

BACKUP DATABASE Chunjae_DaouAuth
  TO DISK='S:\Backup_db\Chunjae_DaouAuth\Chunjae_DaouAuth_backup_20250405.bak'
  WITH COMPRESSION, INIT;

BACKUP DATABASE Chunjae_KOC
  TO DISK='S:\Backup_db\Chunjae_KOC\Chunjae_KOC_backup_20250405.bak'
  WITH COMPRESSION, INIT;

BACKUP DATABASE ChunjaePdf
  TO DISK='S:\Backup_db\ChunjaePdf\ChunjaePdf_backup_20250405.bak'
  WITH COMPRESSION, INIT;

-- ...
```

---

## 4) 인덱스 리빌드 및 실제 소요시간 기록

### 기록 방식

* 실행 전 `SET STATISTICS TIME ON`
* 리빌드 후 출력되는 **CPU 시간 / 경과 시간**을 주석으로 남긴다.

---

### 4-1) Chunjae_ACA

```sql
SET STATISTICS TIME ON
USE Chunjae_ACA
GO

ALTER INDEX PK_QuizTest1_QuizTestHis
ON [dbo].[Tbl_QuizTest1_QuizTestHis]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 453ms, 경과 시간 = 1530ms
*/
```

---

### 4-2) Chunjae_DaouAuth

```sql
SET STATISTICS TIME ON
USE Chunjae_DaouAuth
GO

ALTER INDEX IDX_BIZ_LOG_202410_DEST_PHONE
ON [dbo].[BIZ_LOG_202410]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 140ms, 경과 시간 = 399ms
*/

ALTER INDEX IDX_BIZ_LOG_202412_SEND_PHONE
ON [dbo].[BIZ_LOG_202412]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 313ms, 경과 시간 = 794ms
*/

-- ...
```

---

### 4-3) Chunjae_Main

```sql
SET STATISTICS TIME ON
USE Chunjae_Main
GO

ALTER INDEX im_reject_grp_ext_2_idx
ON [dbo].[im_reject_grp_ext_2]
REBUILD WITH (FILLFACTOR = 80);

/* 소요시간 기재
CPU 시간 = 984ms, 경과 시간 = 1339ms
*/

ALTER INDEX IX_OrderInfo_SName
ON [dbo].[TBL_OrderInfo]
REBUILD WITH (FILLFACTOR = 80);

/* 소요시간 기재
CPU 시간 = 703ms, 경과 시간 = 767ms
*/

ALTER INDEX IX_OrderInfo_SPhone
ON [dbo].[TBL_OrderInfo]
REBUILD WITH (FILLFACTOR = 80);

/* 소요시간 기재
CPU 시간 = 656ms, 경과 시간 = 846ms
*/

ALTER INDEX IX_OrderInfo_STel
ON [dbo].[TBL_OrderInfo]
REBUILD WITH (FILLFACTOR = 80);

/* 소요시간 기재
CPU 시간 = 719ms, 경과 시간 = 901ms
*/

ALTER INDEX IX_TBL_OrderDetail_BookCode_YY
ON [dbo].[TBL_OrderDetail]
REBUILD WITH (FILLFACTOR = 80);

/* 소요시간 기재
CPU 시간 = 6297ms, 경과 시간 = 6984ms
*/
```

---

### 4-4) Chunjae_Pass

```sql
SET STATISTICS TIME ON
USE Chunjae_Pass
GO

ALTER INDEX PK_TBL_Del_Login
ON [dbo].[TBL_Del_Login]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 6781ms, 경과 시간 = 7111ms
*/

ALTER INDEX IX_TBL_Del_Login_UserID2
ON [dbo].[TBL_Del_Login]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 5437ms, 경과 시간 = 5906ms
*/

ALTER INDEX IX_TBL_Del_Login_NickName
ON [dbo].[TBL_Del_Login]
REBUILD WITH (FILLFACTOR = 80)

/* 소요시간 기재
CPU 시간 = 5672ms, 경과 시간 = 5939ms
*/
```


----------------------

## 증적자료

[인덱스 리빌드 대상 리스트, 타팀과 공유]

<img width="2492" height="809" alt="인덱스 리빌드 적용대상리스크 공유내역" src="https://github.com/user-attachments/assets/beb78a9b-6bd4-4b13-9349-974bc9152812" />

[작업계획서]

<img width="734" height="785" alt="image" src="https://github.com/user-attachments/assets/cae2932b-5523-4d48-9544-ef303f3262a1" />

[리빌드 후 결과내역]
<img width="657" height="637" alt="image" src="https://github.com/user-attachments/assets/33ef3f52-46d5-4856-bae8-797eb7a902a7" />







